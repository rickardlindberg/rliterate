WxGuiCodeGenerator {
  file = [ast*:xs] -> { xs }
  ast =
    | ["panel" .:name ast:x] -> {
      %%widget
      "class " name "(wx.Panel, GuiFrameworkBaseMixin):\n" >
        "def __init__(self, parent, **kwargs):\n" >
          "wx.Panel.__init__(self, parent)\n"
          "GuiFrameworkBaseMixin.__init__(self, **kwargs)\n"
        <
        "def _create_gui(self):\n" >
          "first_sizer = None\n"
          "self." %widget " = self\n"
          #create
        <
        "def _update_gui(self):\n" >
          "pass\n"
          #update
        <
        x
      <
    }
    | ["body" [name*:xs] ast:y [ast*:zs]] -> { xs y zs }
    | ["hbox" ast*:xs] -> {
      %%sizer
      @create {
        "self." %sizer " = wx.BoxSizer(wx.HORIZONTAL)\n"
        "if first_sizer is None:\n" >
          "first_sizer = self." %sizer "\n"
          "self.Sizer = first_sizer\n"
        <
      }
      xs
    }
    | ["child" .:x binding:y ast*:zs] -> {
      %%createVars
      %%updateVars
      %%sizerFlag
      %%sizerBorder
      @create {
        "self." %createVars " = {}\n"
        "self." %sizerFlag " = 0\n"
        "self." %sizerBorder " = 0\n"
      }
      @update {
        "self." %updateVars " = {}\n"
      }
      zs
      %%widget
      @create {
        "self." %widget " = " x "(self, **self." %createVars ")\n"
        "self." %sizer ".Add(self." %widget ", flag=self." %sizerFlag ", border=self." %sizerBorder ", proportion=0)\n"
        y
      }
      @update {
        "self." %widget ".UpdateGui(**self." %updateVars ")\n"
      }
    }
    | ["boxspace" ast:x] -> {
      %%sizeritem
      @create {
        "self." %sizeritem " = self." %sizer ".Add(self._get_space_size(self." %sizer ", " x "))\n"
      }
      @update {
        "self." %sizeritem ".SetMinSize(self._get_space_size(self." %sizer ", " x "))\n"
      }
    }
    | ["parameter" .:x ast:y] -> {
      @create { "self." %createVars "[" repr(x) "] = " y "\n" }
      @update { "self." %updateVars "[" repr(x) "] = " y "\n" }
    }
    | ["handler" .:x ast:y] -> {
      @create { "self." %widget ".listen(" repr(x) ", lambda event: " y ")\n" }
    }
    | ["get" astGetList:x] -> { x }
    | ["call" ast:x astList:y] -> { x "(" y ")" }
    | ["name" .:x] -> { x }
    | ["sizerBorder" ast:x [border*:y]] -> { @create { "self." %sizerBorder " = " x "\n" y } }
    | ["sizerExpand"]                   -> { @create { "self." %sizerFlag " |= wx.EXPAND\n" } }
    | ["sizerReserve"]                  -> { @create { "self." %sizerFlag " |= wx.RESERVE_SPACE_EVEN_IF_HIDDEN\n" } }
  border =
    | "ALL"    -> { "self." %sizerFlag " |= wx.ALL\n" }
    | "LEFT"   -> { "self." %sizerFlag " |= wx.LEFT\n" }
    | "RIGHT"  -> { "self." %sizerFlag " |= wx.RIGHT\n" }
    | "TOP"    -> { "self." %sizerFlag " |= wx.TOP\n" }
    | "BOTTOM" -> { "self." %sizerFlag " |= wx.BOTTOM\n" }
  astGetList =
    | ast:x astGetList:y -> { x "." y }
    | ast
  astList = astEmptyList | astNonEmptyList
  astEmptyList = !. -> {}
  astNonEmptyList =
    | ast:x astNonEmptyList:y -> { x ", " y }
    | ast:x
  binding =
    | ["binding" .:x] -> { "self." x " = self." %widget "\n" }
    | .               -> {}
  name = .:x -> {
    "@property\n"
    "def " x "(self):\n" >
      "return self.values[\"" x "\"]\n"
    <
  }
}
