WxGuiCodeGenerator {
  file = [ast*:xs] -> { xs }
  ast =
    | ["panel" .:name ast:x] -> {
      "class " name "(wx.Panel, GuiFrameworkBaseMixin):\n" >
        "def __init__(self, parent, **kwargs):\n" >
          "wx.Panel.__init__(self, parent)\n"
          "GuiFrameworkBaseMixin.__init__(self, **kwargs)\n"
        <
        "def _create_gui(self):\n" >
          "first_sizer = None\n"
          #create
        <
        "def _update_gui(self):\n" >
          #update
        <
        x
      <
    }
    | ["body" [name*:xs] ast:y] -> { xs y }
    | ["hbox" ast*:xs] -> {
      %%sizer
      @create {
        "self." %sizer " = wx.BoxSizer(wx.HORIZONTAL)\n"
        "if first_sizer is None:\n" >
          "first_sizer = self." %sizer "\n"
          "self.Sizer = first_sizer\n"
        <
      }
      xs
    }
    | ["component" .:x astList:y] -> {
      %%component
      @create {
        "self." %component " = " x "(self, " y ")\n"
        "self." %sizer ".Add(self." %component ", proportion=1)\n"
      }
      @update {
        "self." %component ".UpdateGui(" y ")\n"
      }
    }
    | ["parameter" .:x ast:y] -> { x "=" y }
    | ["get" astGetList:x] -> { x }
    | ["call" ast:x astList:y] -> { x "(" y ")" }
    | ["member" .:x] -> { "self." x }
    | ["name" .:x] -> { x }
  astGetList =
    | ast:x astGetList:y -> { x "." y }
    | ast
  astList = astEmptyList | astNonEmptyList
  astEmptyList = !. -> {}
  astNonEmptyList =
    | ast:x astNonEmptyList:y -> { x ", " y }
    | ast:x
  name = .:x -> {
    "@property\n"
    "def " x "(self):\n" >
      "return self.values[\"" x "\"]\n"
    <
  }
}
