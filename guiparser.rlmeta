GuiParser {
  file        = widget*:xs ws !. -> xs
  widget      = ws 'panel' !nameChar name:x ws '{' body:y ws '}' -> ["panel" x y]
  body        = variable*:xs box:y handler*:zs -> ["body" xs y zs]
  variable    = ws '#' name
  box         = boxType:x ws '{' (child|box)*:xs ws '}' -> [x ~xs]
  boxType     = ws 'HBox' -> "hbox"
  child       =
    | box
    | name:x binding:z ws '(' parameter*:xs handler*:ys ws ')' -> ["child" x z xs ys]
  parameter   = name:x ws '=' expr:y -> ["parameter" x y]
  handler     = ws '@' name:x ws '=' expr:y -> ["handler" x y]
  binding     =
    | '[' name:x ']' -> ["binding" x]
    |                -> []
  expr        =
    | expr1:x (ws '.' expr1)*:xs -> ["get" x ~xs]
  expr1       =
    | expr2:x ws '(' expr*:xs ws ')' -> ["call" x ~xs]
    | expr2
  expr2       =
    | name:x -> ["name" x]
  name        = ws nameStart:x nameChar*:xs -> join([x ~xs])
  nameStart   = alpha | '_'
  nameChar    = alphanum | '_'
  alpha       = 'a'-'z' | 'A'-'Z'
  alphanum    = alpha | '0'-'9'
  ws          = (' ' | '\n')*
}
