GuiParser {
  file        = widget*:xs ws !. -> xs
  widget      = ws 'panel' !nameChar name:x ws '{' body:y ws '}' -> ["panel" x y]
  body        = variable*:xs box:y handler*:zs -> ["body" xs y zs]
  variable    = ws '#' name
  box         = ws 'HBox' ws '{' component*:xs ws '}' -> ["hbox" ~xs]
  component   =
    | box
    | name:x options:z ws '(' parameter*:xs handler*:ys ws ')' -> ["component" x z xs ys]
  parameter   = name:x ws '=' expr:y -> ["parameter" x y]
  handler     = ws '@' name:x ws '=' expr:y -> ["handler" x y]
  options     =
    | '[' option*:xs ']' -> xs
    |                    -> []
  option      =
    | name:x -> ["option_name" x]
  expr        =
    | expr1:x (ws '.' expr1)*:xs -> ["get" x ~xs]
  expr1       =
    | expr2:x ws '(' expr*:xs ws ')' -> ["call" x ~xs]
    | expr2
  expr2       =
    | name:x -> ["name" x]
  name        = ws nameStart:x nameChar*:xs -> join([x ~xs])
  nameStart   = alpha | '_'
  nameChar    = alphanum | '_'
  alpha       = 'a'-'z' | 'A'-'Z'
  alphanum    = alpha | '0'-'9'
  ws          = (' ' | '\n')*
}
